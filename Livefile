# CONFIG #########################################

global.config = require './config.default' .cfg

try
  cstm = require './config' .cfg
  config.chok['config.ls'] = type: \cfg
  if cstm.server?port? then config.server.port = cstm.server.port
  if cstm.projects? then for prj, cfg of cstm.projects
    if config.projects[prj]?
      for key, value of cstm.projects[prj] then config.projects[prj][key] = value
    else config.projects[prj] = cfg
catch
  if e.code is \MODULE_NOT_FOUND then console.log 'no custom config'
  else throw e

# OPTIONS ########################################

option \all, \a, \Boolean, 'only for `compile`, force to compile all lofe file'
option \dev, \d, \Boolean, 'only for `compile`, activate dev mode'
option \sel, \s, 'String | [String]', 'only for `compile`, if none then `default`'
option \rel, \r, \Boolean, 'only for release purpose, compress browserified'

# TASKS ##########################################

task \clean, 'remove the `dist` dir', !->
  require \fs .rmSync \dist, { recursive: yes, force: yes }

task \compile, 'compile the selected (basic if none) lofe file', (opts) !->
  if opts.all? then console.log 'going to compile all'
  else
    if opts.sel?
      unless typeof! opts.sel is \Array then opts.sel = [opts.sel]
    else opts.sel = <[default]>
    np = {}; keys = Object.keys config.projects
    for sel in opts.sel
      unless sel in keys then throw new Error "[ERROR]: '#sel' isnt a project!"
      else np[sel] = config.projects[sel]
    config.projects = np
  config.dev = if opts.dev? then opts.dev else no
  config.release = if opts.rel then opts.rel else no
  require './compile' .compile!

task \list, 'list all lofe files configured and ready to be compile', !->
  for key, val of config.projects then console.log "#key => #{val.desc}"

task \serve, 'just launch the server', !->
  require './compile' .serve!
